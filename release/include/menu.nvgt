/* menu.nvgt - create simple menus based on an internal audio form
 * This also contains helper menus which can be used both as examples of the classes usage as well as to aid in game development, such as a learn game sounds menu.
 *
 * NVGT - NonVisual Gaming Toolkit
 * Copyright (c) 2022-2024 Sam Tupy
 * https://nvgt.gg
 * This software is provided "as-is", without any express or implied warranty. In no event will the authors be held liable for any damages arising from the use of this software.
 * Permission is granted to anyone to use this software for any purpose, including commercial applications, and to alter it and redistribute it freely, subject to the following restrictions:
 * 1. The origin of this software must not be misrepresented; you must not claim that you wrote the original software. If you use this software in a product, an acknowledgment in the product documentation would be appreciated but is not required.
 * 2. Altered source versions must be plainly marked as such, and must not be misrepresented as being the original software.
 * 3. This notice may not be removed or altered from any source distribution.
*/

#include "form.nvgt"

class menu {
	funcdef void menu_callback(menu@, string);
	audio_form form; // Only access if really needed.
	// All properties from this point can be freely set while using this class. If a variable is marked as private and begins with an underscore, it only means that public getters and setters with the same name minus the prefixed underscore exist later in the class.
	string intro_text; // Spoken when the void intro() function is called.
	string click_sound; // Played when the user changes positions in the menu.
	string select_sound; // Played when the user chooses an option in the menu.
	string edge_sound; // Plays when the user attempts moving beyond the border of a menu while wrapping is disabled.
	string wrap_sound; // Played when the menu wraps (only happens when bool wrap = true).
	string open_sound; // Played when the void intro() function is called.
	string close_sound; // Played when the user escapes out of the menu.
	string sounds_prefix; // The value set here is prepended to any sound filenames that are loaded. A value could be sounds/, for example.
	string sounds_suffix; // The value set here is appended to any sound filenames that are loaded. A value could be .ogg, for example.
	menu_callback@ background_callback = null; // A function called every time the monitor() method of this class is called.
	string background_callback_data;
	dictionary user_data; // Optional data for this menu that you can do anything you want with, such as storing an extra identifier or piece of information your script wants to use in regards to this menu.
	bool wrap = false; // If the user moves beyond an edge of the menu, set this to true to jump them to the other edge, or false to play an edge sound and repeat the last item.
	uint wrap_delay = 10; // How much time should the menu block when wrapping.
	bool focus_first_item = false; // If this is false, the user will be required to press an arrow key to focus either the first or last item of the menu after the intro function has been called. Otherwise they will be focused on the first item.
	bool automatic_intro = true; // If this is true, the intro function will be automatically called the first time the monitor method is invoqued, then the variable will be set to false. It can be set to true again at any time to cause the intro sequence to repeat, or the intro function can be called manually.
	bool intro_interrupt = true; // This boolean is passed directly to the speak function when speaking the menu's intro, then it's reset to true. You can set it to false to speak a message prior to the next menu intro.
	private bool _repeat_boundary_items = false; // Menu items will not speak when the user repeatedly hits the edge of a non-wrapping menu if this is set to false.
	private bool _allow_escape = true; // If set to false, the user must click on an option manually and will not be able to use escape to exit the menu.
	private bool _allow_go_to_index = true; // If set to false, the user can no longer press ctrl+g to select a menu item by number.
	private bool _allow_search = true; // If set to false, the user can no longer press ctrl+f to search for an item in the menu by text.
	pack@ pack_file; // Optionally set this to a pack containing sounds.
	menu() {
		reset();
	}
	// The following function resets the menu to it's default state. If reset_settings is true, the sounds which have been set and other such options are cleared.
	void reset(bool reset_settings = false) {
		if (reset_settings) {
			intro_text = click_sound = select_sound = edge_sound = wrap_sound = open_sound = close_sound = sounds_prefix = sounds_suffix = "";
			wrap = false;
			automatic_intro = true;
			wrap_delay = 10;
			focus_first_item = false;
			allow_escape = true;
			_repeat_boundary_items = false;
			_allow_escape = true;
			_allow_go_to_index = true;
			_allow_search = true;
			@pack_file = null;
		}
		form.reset();
		form.create_window();
		f_list = form.create_list("Menu", repeat_boundary_items : true);
		form.set_event_callback(f_list, on_control_event_callback(on_form_list_event));
		f_ok = form.create_button("OK", true);
		form.set_state(f_ok, true, false);
		f_cancel = form.create_button("Cancel", false, allow_escape);
		form.set_state(f_cancel, true, false);
		form.focus_silently(f_list);
		//If the settings haven't been reset, restore the settings to the form, as it has been recreated.
		if(!reset_settings) {
			set_allow_escape(get_allow_escape());
			set_allow_search(get_allow_search());
			set_allow_go_to_index(get_allow_go_to_index());
		}
	}
	// Item IDs are entirely optional.
	int add_item(const string&in text, const string&in id = "", int position = -1) {
		if (!form.add_list_item(f_list, text, id, position, false, focus_first_item)) return -1;
		if (position == -1) position = form.get_list_count(f_list) -1;
		return position;
	}
	void add_items(string[]@ item_list) {
		for (uint i = 0; i < item_list.length(); i++) add_item(item_list[i]);
	}
	// Call this function to produce the intro sequence for the menu. You don't need to call it at all, if you like.
	void intro() {
		if (intro_text.empty()) {
			form.focus(f_list);
			return;
		}
		play(open_sound);
		speak(intro_text.format(form.get_list_count(0)), intro_interrupt);
		intro_interrupt = true;
		if (focus_first_item) form.set_list_position(0, 0, true);
		else {
			string text = form.get_list_item(0, form.get_list_position(0));
			if (!text.empty()) speak(text, false);
		}
	}
	// The following function returns true if the menu should keep running or false if it has been exited or if an option has been selected.
	bool monitor() {
		last_selected_index = -1;
		last_selected_id = "";
		if (@background_callback != null) background_callback(this, background_callback_data);
		if (automatic_intro) {
			intro();
			automatic_intro = false;
		}
		form.monitor();
		if (form.is_pressed(f_cancel)) {
			play(close_sound);
			return false;
		}
		else if (!form.is_pressed(f_ok)) return true;
		last_selected_index = form.get_list_position(f_list);
		if (last_selected_index < 0) return true;
		last_selected_id = form.get_list_item_id(f_list, last_selected_index);
		play(select_sound);
		return false;
	}
	// A blocking style of running the menu which acts very much like dynamic_menu. This blocks until the user selects an option or exits the menu, then returns the selected_item property.
	int run() {
		while (monitor()) wait(5);
		return last_selected_index;
	}
	// Various getters and setters.
	int get_focused_item() property {
		return form.get_list_position(f_list);
	}
	void set_focused_item(int index) property {
		form.set_list_position(f_list, index);
	}
	bool set_focused_item(int index, bool speak_change) {
		return form.set_list_position(f_list, index, speak_change);
	}
	bool set_item_text(int index, const string&in new_text) {
		return form.edit_list_item(f_list, new_text, index);
	}
	string get_item_text(int index) {
		return form.get_list_item(f_list, index);
	}
	bool set_item_id(int index, const string&in new_id) {
		return form.edit_list_item_id(f_list, new_id, index);
	}
	string get_item_id(int index) {
		return form.get_list_item_id(f_list, index);
	}
	// Use the following method sparingly as we must loop through all items to determine an index from an ID.
	int get_item_index(const string&in id) {
		return form.get_list_index_by_id(f_list, id);
	}
	int get_item_count() property {
		return form.get_list_count(f_list);
	}
	int get_selected_item() const property {
		return last_selected_index;
	}
	string get_selected_item_id() const property {
		return last_selected_id;
	}
	bool get_repeat_boundary_items() const property {
		return _repeat_boundary_items;
	}
	void set_repeat_boundary_items(bool enabled) property {
		_repeat_boundary_items = enabled;
		form.set_list_properties(f_list, false, enabled);
	}
	bool get_allow_escape() const property {
		return _allow_escape;
	}
	void set_allow_escape(bool enabled) property {
		_allow_escape = enabled;
		form.set_button_attributes(f_cancel, false, enabled);
	}
	bool get_allow_go_to_index() const property {
		return _allow_go_to_index;
	}
	void set_allow_go_to_index(bool enabled) property {
		_allow_go_to_index = enabled;
		form.set_enable_go_to_index(f_list, enabled);
	}
	bool get_allow_search() const property {
		return _allow_search;
	}
	void set_allow_search(bool enabled) property {
		_allow_search = enabled;
		form.set_enable_search(f_list, enabled);
	}
	// Anything beyond this point is only used internally by this class.
	private int f_list, f_ok, f_cancel; // form controls
	private int last_selected_index;
	private string last_selected_id;
	// Simple sound management
	private sound@[] playing_sounds;
	bool play(const string&in soundpath) {
		if (soundpath.empty()) return false;
		sound s;
		if (!s.load(sounds_prefix + soundpath + sounds_suffix, @pack_file != null? @pack_file : @sound_default_pack)) return false;
		bool r = s.play();
		// Clean sounds that have finished playing.
		for (uint i = 0; i < playing_sounds.length(); i++) {
			if (playing_sounds[i].playing) continue;
			playing_sounds.remove_at(i);
			i--;
		}
		playing_sounds.insert_last(s);
		return r;
	}
	private int on_form_list_event(audio_form@ f, int c, control_event_type event, dictionary@ args) {
		if (event != event_list_cursor) return 0;
		if (args.exists("boundary")) {
			if (!wrap) {
				play(edge_sound);
				return 0;
			}
			play(wrap_sound);
			stop_speech();
			wait(wrap_delay);
			f.set_list_position(f_list, int(args["key"]) == KEY_DOWN? 0 : f.get_list_count(0) -1, true);
			return 1;
		}
		play(click_sound);
		return 0;
	}
}

// helper menus:
// learn_game_sounds: takes a dictionary of sound descriptions mapped to sound filenames or lists of sound filenames and displays a menu which allows users to press enter on sound descriptions to hear associated sounds.
void learn_game_sounds_menu(const name_value_collection&in sfx, const string&in audio_prefix = "", menu@ m = null, string go_back = "Go back") {
	sound audio;
	if (@m == null) @m = menu();
	if (m.intro_text.empty()) m.intro_text = "Select a sound with the up and down arrow keys and press enter to preview it.";
	string select_sound = m.select_sound;
	m.select_sound = "";
	for (uint i = 0; i < sfx.size(); i++) m.add_item(sfx.name_at(i));
	m.add_item(go_back);
	m.intro();
	while (true) {
		wait(5);
		int old_pos = m.focused_item;
		bool r = m.monitor();
		if (m.focused_item != old_pos) {
			audio.close();
			old_pos = m.focused_item;
		}
		if (r) continue;
		if (m.selected_item < 0) return;
		if (m.selected_item >= sfx.size()) {
			m.play(select_sound);
			return;
		}
		string filename = sfx.value_at(m.selected_item);
		if (filename.find(",")>-1) {
			string[]@ options = filename.split(",");
			filename = options.random();
		}
		bool loop = filename.starts_with("~");
		if (loop) filename.erase(0, 1);
		if (!filename.empty()) {
			if (filename[0] != "/") filename = audio_prefix + filename;
			else filename.erase(0, 1);
			audio.load(filename);
			loop? audio.play_looped() : audio.play();
		}
		else speak("Unable to play "+sfx.name_at(m.selected_item));
	}
}
// yes_no: A simple yes no menu that returns true if the user clicked yes, false if they clicked no or pressed escape. The prompt is inserted as the first item in the menu so that users can repeat it.
bool yes_no(const string&in prompt, menu@ m = null, string yes = "yes", string no = "no") {
	if (@m == null) @m = menu();
	m.focus_first_item = true;
	m.add_item(prompt);
	m.add_item(yes);
	m.add_item(no);
	speak(prompt);
	m.automatic_intro = false;
	while (m.selected_item < 1) {
		wait(5);
		if (m.monitor()) continue;
		if (m.selected_item == 0) speak(prompt);
		else break;
	}
	return m.selected_item == 1;
}
