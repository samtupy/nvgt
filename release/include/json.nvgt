class json {
	json_object data;
	json() {
		this.reset();
	}
	json(var@ v) {
		this.reset();
		this.data = v;
	}
	void reset() {
		this.data = json_object();
	}
	bool loads(const string&in str) {
		json_object@ j = null;
		try {
			@j = parse_json(str);
		} catch {}
		if (@j == null) return false;
		this.data = j;
		return true;
	}
	bool loadf(const string&in filename) {
		file f;
		if (!f.open(filename, "r")) return false;
		string r = f.read();
		f.close();
		return this.loads(r);
	}
	bool loadds(datastream@ d) {
		if (@d == null || !d.active) return false;
		return this.loads(d.read());
	}
	string dump(int indent = 0, int step = -1) const {
		return this.data.stringify(indent, step);
	}
	bool save(const string&in filename, int indent = 0, int step = -1) {
		file f;
		if (!f.open(filename, "w")) return false;
		if (this.size() < 1) return false;
		f.write(this.dump(indent, step));
		f.close();
		return true;
	}
	var@ get(const string&in key, var@ def = null) const {
		return this.data.get(key, def);
	}
	bool set(const string&in key, var@ value) {
		this.data.set(key, value);
		return true;
	}
	bool set(const string&in key, json@ value) {
		if (@value == null || value.size() < 1) return false;
		var@ j = value.data;
		return this.set(key, j);
	}
	bool exists(const string&in key) const {
		return this.data.exists(key);
	}
	bool remove(const string&in key) {
		if (!this.exists(key)) return false;
		this.data.remove(key);
		return true;
	}
	void clear() {
		this.data.clear();
	}
	uint size() const {
		return this.data.size();
	}
	string[] get_keys() const property {
		return this.data.get_keys();
	}
	string[] get_keys_recursive() const property {
		string[] f = this.list_keys_recursive("", @this.data, true);
		for (uint i = 0; i < f.length(); i++) {
			if (!this.is_object(f[i])) continue;
			json_object@ j = this.get(f[i]);
			if (@j == null) continue;
			f.extend(this.list_keys_recursive(f[i], @j, false));
		}
		return f;
	}
	bool is_array(const string&in key) const {
		return this.data.is_array(key);
	}
	bool is_null(const string&in key) const {
		return this.data.is_null(key);
	}
	bool is_object(const string&in key) const {
		return this.data.is_object(key);
	}
	var@ opCall(const string&in path, var@ def = null) const {
		return this.data(path, def);
	}
	var@ get_opIndex(const string&in key) const property {
		return this.data.get_opIndex(key);
	}
	void set_opIndex(const string&in key, var@ value) property {
		this.data.set_opIndex(key, value);
	}
	private string[] list_keys_recursive(const string&in key, const json_object@ v, bool top = true) const {
		string[] f;
		string[] k = v.get_keys();
		if (top) f = k;
		else {
			for (uint i = 0; i < k.length(); i++) {
				if (!v.is_object(k[i])) {
					f.insert_last(key + "." + k[i]);
				} else {
					json_object@ j = v.get(k[i]);
					if (@j == null) continue;
					f.extend(this.list_keys_recursive(key + "." + k[i], @j, false));
				}
			}
		}
		return f;
	}
}