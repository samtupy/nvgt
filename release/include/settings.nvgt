/* settings.nvgt - High level classes to quicly save and reload user data.
 * This system was designed to replace the now empty settings object provided in BGT, however it has been rewritten and it is now quite flexible.
 * Unlike the original settings object in BGT, this does not write to the windows registry, but supports saving your data in a serialized dictionary, INI format or JSON. It also includes some new functionality and changes.
  * The all_users bool in the setup function is now bool local_path and controls where settings are saved. If this is set to true, settings will be in the same directory the program is being ran from. If set to false, settings will be stored in the application data directory under /company/product.
  * added an extra argument to the setup function (string format) that will determine the format the save will use. Valid formats are ini, json and nvgt. Specifying the nvgt format will use a serialized dictionary.
  * Added bool set_encryption(string&in key) which enables encryption for that settings file. Make sure that you call this function before calling the setup function if you wish to use encryption.
  * Added new instant_save property that will determine if data instantly saves when calling the write_string or write_number functions. This is enabled by default, and will instantly save the data to disk.
  * read_string and read_number can now have custom default values.
  * Adds new dump function that will save all configuration data to disk. Returns true on success, false otherwise.
 *
 * NVGT - NonVisual Gaming Toolkit
 * Copyright (c) 2022-2024 Sam Tupy
 * https://nvgt.gg
 * settings.nvgt - Copyright (C) 2024 Ivan Soto
 * This software is provided "as-is", without any express or implied warranty. In no event will the authors be held liable for any damages arising from the use of this software.
 * Permission is granted to anyone to use this software for any purpose, including commercial applications, and to alter it and redistribute it freely, subject to the following restrictions:
 * 1. The origin of this software must not be misrepresented; you must not claim that you wrote the original software. If you use this software in a product, an acknowledgment in the product documentation would be appreciated but is not required.
 * 2. Altered source versions must be plainly marked as such, and must not be misrepresented as being the original software.
 * 3. This notice may not be removed or altered from any source distribution.
*/

#include"ini.nvgt"

class settings_helper {
	string company_name, product;
	private bool can_encrypt_data = false;
	bool local_path;
	string[] is_bool_list = {"1", "0", "t", "true", "false", "on", "off", "enabled", "disabled", "yes", "no"};
	settings@parent;
	settings_helper(settings@ parent) {
		@this.parent = parent;
	}
	bool get_active() property {
		return this.is_active();
	}
	bool is_active() {
		return false;
	}
	bool get_encrypt_data() const property {
		return (!this.encryption_key.empty() and this.can_encrypt_data);
	}
	string get_encryption_key() const property {
		if (@this.parent == null) return "";
		return this.parent.encryption_key;
	}
	bool get_instant_save() const property {
		if (@this.parent == null) return true;
		return this.parent.instant_save;
	}
	bool setup(string company, string product, bool local_path) {
		this.company_name = company;
		this.product = product;
		this.local_path = local_path;
		bool ensure = ensure_data_path(this.get_data_path(), true);
		if (ensure) return this.open();
		else return false;
	}
	string raw_dump() {
		return "";
	}
	string get_data_path() {
		string temppath;
		if (this.local_path) temppath = join({this.company_name, this.product, this.parent.filename + this.get_file_extension()}, "/");
		else temppath = join({get_preferences_path(this.company_name, this.product), this.parent.filename + this.get_file_extension()}, "/");
		string ret = spec::path(temppath).to_string();
		return ret;
	}
	string get_file_extension() {
		return "";
	}
	string[]@list_keys() {
		return {};
	}
	bool clear() {
		return false;
	}
	bool close(bool save = true) {
		return false;
	}
	bool open() {
		return false;
	}
	bool exists(const string&in key) {
		return false;
	}
	bool remove_value(const string&in key) {
		return false;
	}
	bool write_string(const string&in key, const string&in value) {
		return false;
	}
	bool write_number(const string&in key, double number) {
		return false;
	}
	bool write_bool(const string&in key, bool value) {
		return false;
	}
	string read_string(const string&in key) {
		return "";
	}
	double read_number(const string&in key) {
		return parse_float(this.read_string(key));
	}
	bool read_bool(const string&in key) {
		return false;
	}
	var@get_value(string&in key) {
		var v;
		return v;
	}
	bool dump() {
		if (!this.active) return false;
		string filepath = this.get_data_path();
		if (!ensure_data_path(filepath, (this.local_path?false:true))) return false;
		string final_data = this.raw_dump();
		file tempfile;
		if (!tempfile.open(filepath, "wb")) return false;
		if (this.encrypt_data) final_data = string_aes_encrypt(final_data, this.encryption_key);
		tempfile.write(final_data);
		tempfile.close();
		return true;
	}
}
funcdef settings_helper@ settings_helper_factory(settings@ parent);

class ini_settings_helper: settings_helper {
	ini@config;
	ini_settings_helper(settings@parent) {
		super(@parent);
		this.can_encrypt_data = true;
	}
	bool is_active() {
		return @this.config != null;
	}
	bool open() {
		@this.config = ini();
		string filepath = this.get_data_path();
		if (!file_exists(filepath))
			return true;
		file tempfile;
		tempfile.open(filepath, "rb");
		string final_data = tempfile.read();
		tempfile.close();
		if (final_data.empty())
			return true;
		if (this.encrypt_data) final_data = string_aes_decrypt(final_data, this.encryption_key);
		bool loaded = this.config.load_string(final_data, filepath);
		return loaded;
	}
	bool clear() {
		return this.config.clear_section("");
	}
	string[]@list_keys() {
		return this.config.list_keys("");
	}
	string raw_dump() {
		return this.config.dump();
	}
	string get_file_extension() {
		return ".ini";
	}
	bool write_string(const string&in key, const string&in value) {
		if (!this.active) return false;
		bool set = this.config.set_string("", key, value);
		bool save = (this.instant_save ? this.dump() : true);
		return (set and save);
	}
	bool write_bool(const string&in key, bool value) {
		if (!this.active) return false;
		bool set = this.config.set_bool("", key, value);
		bool save = (this.instant_save ? this.dump() : true);
		return (set and save);
	}
	bool write_number(const string&in key, double number) {
		if (!this.active) return false;
		bool set = this.config.set_double("", key, number);
		bool save = (this.instant_save ? this.dump() : true);
		return (set and save);
	}
	string read_string(const string&in key) {
		if (!this.active) return "";
		return this.config.get_string("", key);
	}
	var@get_value(string&in key) {
		if (!this.exists(key)) return var();
		string v = this.read_string(key);
		int f = is_bool_list.find(v.lower());
		if (f > -1)
			return this.read_bool(key);
		else if (v.is_digits()) return parse_int(v);
		else return v;
	}
	bool read_bool(const string&in key) {
		if (!this.active) return false;
		return this.config.get_bool("", key);
	}
	bool exists(const string&in key) {
		if (!this.active) return false;
		return this.config.key_exists("", key);
	}
	bool remove_value(const string&in key) {
		if (!this.active) return false;
		return this.config.delete_key("", key);
	}
	bool close(bool save = true) {
		if (save and !this.dump()) return false;
		this.config.reset();
		return true;
	}
}
settings_helper@ ini_settings_factory(settings@ parent) { return ini_settings_helper(parent); }

class json_settings_helper: settings_helper {
	json_object@config;
	json_settings_helper(settings@parent) {
		super(@parent);
		this.can_encrypt_data = true;
	}
	bool is_active() {
		return @this.config != null;
	}
	bool open() {
		string filepath = this.get_data_path();
		if (!file_exists(filepath)) {
			@this.config = json_object();
			return true;
		}
		file tempfile;
		tempfile.open(filepath, "rb");
		string final_data = tempfile.read();
		tempfile.close();
		if (this.encrypt_data) final_data = string_aes_decrypt(final_data, this.encryption_key);
		@this.config = parse_json(final_data);
		return true;
	}
	bool clear() {
		this.config.clear();
		return true;
	}
	string[]@list_keys() {
		return this.config.get_keys();
	}
	string get_file_extension() {
		return ".json";
	}
	bool write_string(const string&in key, const string&in value) {
		if (!this.active) return false;
		this.config.set(key, value);
		return (this.instant_save ? this.dump() : true);
	}
	bool write_number(const string&in key, double number) {
		if (!this.active) return false;
		this.config.set(key, number);
		return (this.instant_save ? this.dump() : true);
	}
	bool write_bool(const string&in key, bool value) {
		if (!this.active) return false;
		this.config.set(key, value);
		return (this.instant_save ? this.dump() : true);
	}
	bool read_bool(const string&in key) {
		return bool(this.config[key]);
	}
	var@get_value(string&in key) {
		if (!this.exists(key)) return var();
		return this.config[key];
	}
	string read_string(const string&in key) {
		return string(this.config[key]);
	}
	bool exists(const string&in key) {
		if (!this.active) return false;
		return this.config.exists(key);
	}
	bool remove_value(const string&in key) {
		if (!this.active) return false;
		if (!this.config.exists(key)) return false;
		this.config.remove(key);
		return true;
	}
	string raw_dump() {
		return this.config.stringify();
	}
	bool close(bool save = true) {
		if (save and !this.dump()) return false;
		this.config.clear();
		return true;
	}
}
settings_helper@ json_settings_factory(settings@ parent) { return json_settings_helper(parent); }

class nvgt_settings_helper: settings_helper {
	dictionary@config;
	nvgt_settings_helper(settings@parent) {
		super(@parent);
		this.can_encrypt_data = true;
	}
	bool is_active() {
		return @this.config != null;
	}
	bool open() {
		string filepath = this.get_data_path();
		if (!file_exists(filepath)) {
			@this.config = dictionary();
			return true;
		}
		file tempfile;
		tempfile.open(filepath, "rb");
		string final_data = tempfile.read();
		if (this.encrypt_data) final_data = string_aes_decrypt(final_data, this.encryption_key);
		@this.config = deserialize(final_data);
		tempfile.close();
		return true;
	}
	bool clear() {
		this.config.delete_all();
		return true;
	}
	string[]@list_keys() {
		return this.config.get_keys();
	}
	string get_file_extension() {
		return ".dat";
	}
	bool write_string(const string&in key, const string&in value) {
		if (!this.active) return false;
		this.config.set(key, value);
		return (this.instant_save ? this.dump() : true);
	}
	bool write_number(const string&in key, double number) {
		if (!this.active) return false;
		this.config.set(key, number);
		return (this.instant_save ? this.dump() : true);
	}
	var@get_value(string&in key) {
		if (!this.exists(key)) return var();
		string v;
		if (!this.config.get(key, v)) {
			double d;
			if (this.config.get(key, d)) return d;
		}
		int f = is_bool_list.find(v.lower());
		if (f > -1)
			return this.read_bool(key);
		return v;
	}
	string read_string(const string&in key) {
		if (!this.active) return "";
		string ret = string(this.config[key]);
		return ret;
	}
	double read_number(const string&in key) {
		if (!this.active) return 0;
		double ret;
		this.config.get(key, ret);
		return ret;
	}
	bool write_bool(const string&in key, bool value) {
		if (!this.active) return false;
		this.config.set(key, value);
		return (this.instant_save ? this.dump() : true);
	}
	bool read_bool(const string&in key) {
		if (!this.active) return false;
		bool r;
		this.config.get(key, r);
		return r;
	}
	bool exists(const string&in key) {
		if (!this.active) return false;
		return this.config.exists(key);
	}
	bool remove_value(const string&in key) {
		if (!this.active) return false;
		return this.config.delete(key);
	}
	string raw_dump() {
		return this.config.serialize();
	}
	bool close(bool save = true) {
		if (save and !this.dump()) return false;
		this.config.delete_all();
		return true;
	}
}
settings_helper@ nvgt_settings_factory(settings@ parent) { return nvgt_settings_helper(parent); }

dictionary available_settings_formats = {{"ini", @ini_settings_factory}, {"json", @json_settings_factory}, {"nvgt", @nvgt_settings_factory}};

class settings {
	string company_name, product;
	bool local_path = false;
	settings_helper@data_helper;
	string encryption_key;
	bool instant_save = true;
	private string settings_filename;
	settings() {
	}
	bool get_active() const property {
		if (@this.data_helper == null) return false;
		return this.data_helper.active;
	}
	bool set_encryption(const string&in key) {
		this.encryption_key = key;
		return true;
	}
	var@get_opIndex(string&in key) property {
		if (!this.active) throw("Uninitialized settings");
		return this.data_helper.get_value(key);
	}
	void set_opIndex(string key, var value) property {
		if (value.is_string) this.write_string(key, value);
		else if (value.is_boolean) this.write_bool(key, value);
		else if (value.is_numeric) this.write_number(key, value);
		else throw ("Unsupported value");
	}
	bool close(bool save_data = true) {
		if (!this.active) return false;
		return this.data_helper.close(save_data);
	}
	bool exists(const string&in key) {
		if (!this.active) return false;
		return this.data_helper.exists(key);
	}
	string read_string(const string&in key, const string&in default_value = "") {
		if (!this.exists(key)) return default_value;
		return this.data_helper.read_string(key);
	}
	double read_number(const string&in key, double default_value = 0) {
		if (!this.exists(key)) return default_value;
		return this.data_helper.read_number(key);
	}
	bool write_string(const string&in key, const string&in value) {
		if (!this.active) return false;
		return this.data_helper.write_string(key, value);
	}
	bool write_number(const string&in key, double number) {
		if (!this.active) return false;
		return this.data_helper.write_number(key, number);
	}
	bool write_bool(string&in key, bool value) {
		if (!this.active) return false;
		return this.data_helper.write_bool(key, value);
	}
	bool read_bool(string&in key, bool default_value = false) {
		if (!this.active) return default_value;
		if (!this.exists(key)) return default_value;
		return this.data_helper.read_bool(key);
	}
	bool has_other_products() {
		if (!this.active) return false;
		string[]@ path = this.data_helper.get_data_path().split("/");
		path.remove_range(path.length() - 2, path.length() - 1);
		string final_path = join(path, "/");
		return find_directories(final_path + "/*").length() > 1;
	}
	bool remove_product() {
		if (!this.active) return false;
		string file_path = this.data_helper.get_data_path();
		if (!file_delete(file_path)) return false;
		string[]@ path = file_path.split("/");
		path.remove_at(path.length() - 1);
		if (this.has_other_products()) path.remove_at(path.length() - 1);
		return directory_delete(join(path, "/"));
	}
	bool clear(bool completely = true) {
		if (!this.active) return false;
		bool c = this.data_helper.clear();
		if (!c or !completely) return c;
		return file_delete(this.data_helper.get_data_path());
	}
	string get_raw_dump() {
		if (!this.active) return "";
		return this.data_helper.raw_dump();
	}
	string[]@keys() {
		if (!this.active) return {};
		return this.data_helper.list_keys();
	}
	void set_filename(string&in fname) property {
		if (this.active) return;
		this.settings_filename = fname;
	}
	string get_filename() property {
		if (this.settings_filename.empty()) return "settings";
		return this.settings_filename;
	}
	bool remove_value(const string&in value_name) {
		if (!this.exists(value_name)) return false;
		return this.data_helper.remove_value(value_name);
	}
	bool dump() {
		if (!this.active) return false;
		return this.data_helper.dump();
	}
	bool setup(const string company, const string product, const bool local_path, const string format = "ini") {
		this.company_name = company;
		this.product = product;
		this.local_path = local_path;
		settings_helper_factory@ factory = cast < settings_helper_factory@ > (available_settings_formats[format]);
		settings_helper@ temp = @factory != null ? factory(this) : null;
		if (@temp == null) {
			throw ("unsupported settings format");
			return false;
		}
		bool success = temp.setup(company, product, local_path);
		if (success) @this.data_helper = temp;
		return success;
	}
}
bool ensure_data_path(const string&in filename, bool absolute = true) {
	spec::path filePath;
	if (!filePath.parse(filename)) {
		return false;
	}
	filePath.make_file();
	spec::path dirPath;
	if(!absolute)  dirPath = filePath.get_parent();
	else dirPath = filePath.absolute();
	spec::path currentPath;
	for (int i = 0; i < dirPath.depth; i++) {
		currentPath.push_directory(dirPath[i]);
		string fullPath = (system_is_windows and absolute? currentPath.device+"\\":"") + currentPath.to_string(spec::PATH_STYLE_NATIVE);
		if (!directory_exists(fullPath)) {
			if (!directory_create(fullPath)) {
				return false;
			}
		}
	}
	return true;
}
