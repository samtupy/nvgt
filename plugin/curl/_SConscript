Import("env")

if env["PLATFORM"] == "linux":
	# At least on my Ubuntu installations, the curl headers end up in what might be a somewhat nonstandard place, at any rate it doesn't hurt to add the edgecase here.
	env.Append(CPPPATH = ["/usr/include/x86_64-linux-gnu"])
scriptarray = env.SharedObject("scriptarray", "#plugin/ASAddon/scriptarray.cpp", LIBPATH = [])
winlibs = ""
if env["PLATFORM"] == "win32":
	winlibs = "kernel32 user32 crypt32 advapi32 iphlpapi netapi32 uuid wldap32 ws2_32 normaliz"
if ARGUMENTS.get("no_shared_plugins", "0") == "0":
	env.SharedLibrary("#release/lib/nvgt_curl", ["internet.cpp", scriptarray], LIBS = Split(winlibs) + ["libcurl", "PocoFoundationMT" if env["PLATFORM"] == "win32" else "PocoFoundation"], CPPDEFINES = ["CURL_STATICLIB"])
static = env.Object("internet_static", "internet.cpp", CPPDEFINES = [("NVGT_PLUGIN_STATIC", "nvgt_curl"), "CURL_STATICLIB"])
static = env.StaticLibrary("#build/lib/nvgt_curl", [static], LIBS = ["libcurl"])
static = [static, "libcurl"]
Return("static")
